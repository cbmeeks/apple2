         ORG   $800       ; VIDEO DECODER PROGRAM START

                          ;
                          ;
                          ; VARIOUS EQUATES RELATING
                          ; TO STORAGE AND RETRIEVAL
                          ;
                          ;

HIRES1   EQU   $2000      ; HIRES PAGES 1 AND 2
HIRES2   EQU   $4000

UPDATE1  EQU   $6000      ; FRAME UPDATES 1 AND 2
UPDATE2  EQU   $8000      ; *2 ONLY HOLDS ABOUT 5KB

LN       EQU   $B00       ; LOCATION WHERE FRAME LENGTHS
                          ; ARE STORED

RDLO     EQU   $06        ; LOCATION IN MEMORY WE
RDHI     EQU   $07        ; ARE READING FROM

WRTLO    EQU   $08        ; PAGE LOCATION WE ARE
WRTHI    EQU   $09        ; WRITING TO

LNLO     EQU   $EC        ; FOR READING UPDATE LENGTHS
LNHI     EQU   $ED

STATE    EQU   $EB        ; HOLDS PROGRAM STATE

                          ;
                          ;
                          ; EQUATES RELATING TO
                          ; SYSTEM CALLS
                          ;
                          ;

MLI      EQU   $BF00      ; PRODOS CALL
CREATE   EQU   $C0        ; MLI CREATE FILE
OPEN     EQU   $C8        ; MLI OPEN FILE
READ     EQU   $CA        ; MLI READ FILE
WRITE    EQU   $CB        ; MLI WRITE FILE
SETPOS   EQU   $CE        ; MLI SET POSITION IN FILE
CLOSE    EQU   $CC        ; MLI CLOSE FILE
DESTROY  EQU   $C1        ; MLI DELETE FILE
SETPFIX  EQU   $C6        ; MLI GET SYSTEM PREFIX
GETPFIX  EQU   $C7        ; MLI SET SYSTEM PREFIX

PRNTA    EQU   $FDDA      ; PRINT ACCUMULATOR
CROUT    EQU   $FD8E      ; OUTPUT CARRIAGE RETURN

                          ;
                          ;
                          ; MAIN PROGRAM AREA
                          ;
                          ;

         JSR   SETPRE

START    JSR   ASKFILE

         JSR   OPNFILE

         LDY   #0

INIT     LDA   #0         ; PUT JUST A 'SKIP FRAME' IN UPDATE2
         STA   UPDATE2

         LDA   #1
         STA   STATE      ; SET STATE = 1

         JSR   LDFRAME1
         JSR   LDLNS

         LDA   #<LN       ; INIT READ BYTES FOR UPDATE LENGTH
         STA   LNLO
         LDA   #>LN
         STA   LNHI

         JSR   CRETEMP    ; CREATE TEMP VIDEO CACHE FILE
         JSR   OPNTEMP

         LDA   #$00       ; SET UP BUFFER THROUGH WHICH TO COPY TEMP CACHE
         STA   FLOC
         LDA   #$60
         STA   FLOC+1
         LDA   #$00
         STA   FLEN
         LDA   #$10
         STA   FLEN+1

TMPLOOP  JSR   RDFILE     ; COPY PART OF VIDEO FILE INTO CACHE
         JSR   WRTEMP
         LDA   #$00
         CMP   FTRANS
         BNE   TMPDONE
         LDA   #$10
         CMP   FTRANS+1
         BEQ   TMPLOOP

TMPDONE  JSR   CLSTEMP

         JSR   CLSFILE

         JSR   OPNTEMP

         STA   $C057      ; INIT DISPLAY(?) -- INSPECT FURTHER
         STA   $C050

         STA   $C054

LOOP     JSR   UPDATE     ; MAIN LOOP
         LDA   $C000      ; READ FOR KEYPRESS
         BMI   EXIT
         STA   $C010      ; CLEAR KEYBOARD STROBE
         JMP   LOOP

EXIT     JSR   CLSTEMP
         JSR   DESTEMP
         JMP   $FA62      ; SWITCH TEXT

                          ;
                          ;
                          ; HIGH-LEVEL SUBROUTINES
                          ;
                          ;

ASKFILE  LDX   #0         ; ASK WITCH VIDEO FILE TO PLAY
:LOOP1   INX              ; LOOP TO PRINT 'FILENAME:'
         LDA   PROMPT,X
         JSR   $FDF0
         CPX   PROMPT
         BLT   :LOOP1
         LDA   #$A0       ; SET ' :' AS PROMPT
         STA   $33
         JSR   $FD6A      ; APPLE II GETLN ROUTINE
         STX   FILENAME
         LDX   #0
:LOOP2   LDA   $200,X
         STA   FILENAME+1,X
         INX
         CPX   FILENAME
         BNE   :LOOP2
         RTS

UPDATE   LDA   STATE      ; HANDLES UPDATING, SWITCHING FRAMES
         BNE   :NOTZERO
         JSR   WRTHR1
         JSR   RDUP1
         JSR   PROCUP
         LDA   #1
         STA   STATE
         STA   $C054
         JMP   :ENDSUB
:NOTZERO LDA   #<UPDATE1  ; STATE = 1, READ NEXT FRAME TO UPD1
         STA   RDPARST+2
         LDA   #>UPDATE1
         STA   RDPARST+3
         JSR   NXTLN
         JSR   RDTEMP
         JSR   WRTHR2
         JSR   RDUP1
         JSR   PROCUP
         LDA   #0
         STA   STATE
         STA   $C055
:ENDSUB  RTS

LDFRAME1 LDA   #$00       ; LOAD FRAME 1 INTO HIRES 1 AND 2
         STA   RDPARSF+4  ; SETTING READ LENGTH
         LDA   #$20
         STA   RDPARSF+5
         LDA   #<HIRES1
         STA   RDPARSF+2  ; LOAD INTO HIRES 1
         LDA   #>HIRES1
         STA   RDPARSF+3
         JSR   RSTFILE
         JSR   RDFILE
         LDA   #<HIRES2   ; LOAD INTO HIRES 2
         STA   RDPARSF+2
         LDA   #>HIRES2
         STA   RDPARSF+3
         JSR   RSTFILE
         JSR   RDFILE
         RTS

LDLNS    LDA   #$00       ; LOAD LIST OF UPDATE LENGTHS
         STA   RDPARSF+4
         LDA   #$01
         STA   RDPARSF+5
         LDA   #<LN
         STA   RDPARSF+2
         LDA   #>LN
         STA   RDPARSF+3
         JSR   RDFILE
         RTS

                          ;
                          ;
                          ; MID-LEVEL SUBROUTINES
                          ;
                          ;

PROCUP   LDA   (RDLO),Y   ; PROCESS DIFFS, GAPS IN AN 'UPDATE' SECTION
         BEQ   :ENDSUB    ; A ZERO INDICATES END OF UPDATE
         AND   #$80
         BNE   :NOGAP     ; IF MSB (BIT 7) NOT SET, THIS IS A GAP
         LDA   WRTLO      ; INCREMENT WRITE LOCATION WITH GAP
         CLC
         ADC   (RDLO),Y
         STA   WRTLO
         LDA   WRTHI
         ADC   #0         ; ADD CARRY TO HIGH BYTE
         STA   WRTHI
         INC   RDLO
         BNE   :NORO1
         INC   RDHI
:NORO1   JMP   PROCUP
:NOGAP   LDA   (RDLO),Y
         STA   (WRTLO),Y  ; WRITE UPDATED BYTE
         INC   WRTLO
         BNE   :NORO2
         INC   WRTHI
:NORO2   INC   RDLO
         BNE   :NORO3
         INC   RDHI
:NORO3   JMP   PROCUP
:ENDSUB  LDY   #0
         RTS

                          ;
                          ;
                          ; LOW-LEVEL SUBROUTINES
                          ;
                          ;

INCRD    INC   RDLO       ; INCREASE READ LOCATION
         BNE   :NOROLL
         INC   RDHI
:NOROLL  RTS

INCWRT   INC   WRTLO      ; INCREASE WRITE LOCATION
         BNE   :NOROLL
         INC   WRTHI
:NOROLL  RTS

RDUP1    LDA   #$60       ; BEGIN READING UPDATE1
         STA   RDHI
         LDA   #$00
         STA   RDLO
         RTS

RDUP2    LDA   #$80       ; BEGIN READING UPDATE2
         STA   RDHI
         LDA   #$00
         STA   RDLO
         RTS

WRTHR1   LDA   #$20       ; BEGIN WRITING HIRES1
         STA   WRTHI
         LDA   #$00
         STA   WRTLO
         RTS

WRTHR2   LDA   #$40       ; BEGIN WRITING HIRES2
         STA   WRTHI
         LDA   #$00
         STA   WRTLO
         RTS

NXTLN    LDA   (LNLO),Y   ; STORE UPDATE LENGTH IN RDPARST, RESET IF LAST FRAME
         LDY   #1
         CLC
         ADC   (LNLO),Y
         BNE   :NOTEND
         LDA   #<LN       ; INIT READ BYTES FOR UPDATE LENGTH
         STA   LNLO
         LDA   #>LN
         STA   LNHI
         JSR   RSTTEMP
:NOTEND  LDY   #0
         LDA   (LNLO),Y
         STA   RDPARST+4
         INC   LNLO
         BNE   :NOROLL
         INC   LNHI
:NOROLL  LDA   (LNLO),Y
         STA   RDPARST+5
         INC   LNLO       ; SET UP NEXT READ
         BNE   :NORO1
         INC   LNHI
:NORO1   RTS

CRETEMP  JSR   MLI
         DFB   CREATE
         DW    CREPARST
         BNE   :ERROR     ; FIXING A BAD BRANCH?
         JMP   :ENDSUB
:ERROR   JMP   ERROR
:ENDSUB  RTS

OPNFILE  JSR   MLI        ; OPEN VIDEO FILE
         DFB   OPEN
         DW    OPNPARSF
         BNE   ERROR
         LDA   OPNPARSF+5  ; STORE REF NUM
         STA   RDPARSF+1
         STA   POSPARSF+1
         STA   CLSPARSF+1
         RTS

OPNTEMP  JSR   MLI        ; OPEN TEMP FILE
         DFB   OPEN
         DW    OPNPARST
         BNE   ERROR
         LDA   OPNPARST+5  ; STORE REF NUM
         STA   RDPARST+1
         STA   WRPARST+1
         STA   POSPARST+1
         STA   CLSPARST+1
         RTS

RDFILE   JSR   MLI        ; READ VIDEO FILE
         DFB   READ       ; (SET DKRDLN, DKSTORE FIRST)
         DW    RDPARSF
         BNE   ERROR
         RTS

RDTEMP   JSR   MLI        ; READ TEMP FILE
         DFB   READ       ; (SET DKRDLN, DKSTORE FIRST)
         DW    RDPARST
         BNE   ERROR
         RTS

WRTEMP   JSR   MLI
         DFB   WRITE
         DW    WRPARST
         BNE   ERROR
         RTS

RSTFILE  JSR   MLI        ; RESET VIDEO FILE MARK TO BEGINNING
         DFB   SETPOS
         DW    POSPARSF
         BNE   ERROR
         RTS

RSTTEMP  JSR   MLI        ; RESET TEMP FILE MARK TO BEGINNING
         DFB   SETPOS
         DW    POSPARST
         BNE   ERROR
         RTS

CLSFILE  JSR   MLI        ; CLOSE VIDEO FILE
         DFB   CLOSE
         DW    CLSPARSF
         RTS

CLSTEMP  JSR   MLI        ; CLOSE TEMP FILE
         DFB   CLOSE
         DW    CLSPARST
         RTS

DESTEMP  JSR   MLI
         DFB   DESTROY
         DW    DESPARST
         BNE   ERROR
         RTS

GETPRE   JSR   MLI
         DFB   GETPFIX
         DW    PFIXPARS
         BNE   ERROR
         RTS

SETPRE   JSR   MLI
         DFB   SETPFIX
         DW    PFIXPARS
         BNE   ERROR
         RTS

ERROR    JSR   PRNTA
         JSR   CROUT
         JMP   $FA62
                          ;
                          ;
                          ; PARAMETER LISTS FOR FILE OPS
                          ;
                          ;

CREPARST DFB   7          ; NUMBER OF PARAMETERS FOR 'CREATE'
         DW    TEMPNAME   ; POINT TO NAME OF FILE
         DFB   $C3        ; ACCESS (D RN W R, ENABLED)
         DFB   $06        ; FILE TYPE ($06 = BINARY)
         DW    $00000     ; AUX TYPE (NOT NEEDED)
         DFB   $01        ; THIS IS A FILE, NOT A DIRECTORY ($0D)
         DW    $0000      ; UNUSED DATE STUFF
         DW    $0000


OPNPARSF DFB   3          ; NUMER OF PARAMETERS FOR 'OPEN'
         DW    FILENAME   ; POINTER TO NAME OF FILE
         DW    $0C00      ; I/O BUFFER LOCATION
         DS    1          ; REFERENCE NUMBER FOR FILE

OPNPARST DFB   3          ; NUMER OF PARAMETERS FOR 'OPEN'
         DW    TEMPNAME   ; POINTER TO NAME OF FILE
         DW    $8000      ; I/O BUFFER LOCATION
         DS    1          ; REFERENCE NUMBER FOR FILE


RDPARSF  DFB   4          ; NUMBER OF PARAMETERS FOR 'READ'
         DS    1          ; REF NUM
FLOC     DS    2          ; LOCATION TO LOAD WITH DISK DATA
FLEN     DS    2          ; HOW MUCH TO READ
FTRANS   DS    2          ; WHERE NUM BYTES TRANFERRED IS STORED

RDPARST  DFB   4          ; NUMBER OF PARAMETERS FOR 'READ'
         DS    1          ; REF NUM
         DS    2          ; LOCATION TO LOAD WITH DISK DATA
         DS    2          ; HOW MUCH TO READ
         DS    2          ; WHERE NUM BYTES TRANFERRED IS STORED

WRPARST  DFB   4          ; NUMBER OF PARAMETERS FOR 'WRITE'
         DS    1          ; REF NUM
         DW    $6000      ; POINTS TO BEGINNING OF DATA TO BE TRANSFERRED
         DW    $1000      ; TRANSFER REQUEST, IN BYTES
         DS    2          ; BYTES TRANSFERRED

POSPARSF DFB   2          ; NUMBER OF PARAMETERS FOR 'SET MARK'
         DS    1          ; REF NUM
         DFB   0          ; VALUE FOR MARK (ALWAYS BEGINNING OF FILE)
         DFB   0
         DFB   0          ; (COULD MAYBE USE 'DS 3')

POSPARST DFB   2          ; NUMBER OF PARAMETERS FOR 'SET MARK'
         DS    1          ; REF NUM
         DFB   0          ; VALUE FOR MARK (ALWAYS BEGINNING OF FILE)
         DFB   0
         DFB   0          ; (COULD MAYBE USE 'DS 3')

CLSPARSF DFB   1          ; NUMBER OF PARAMETERS FOR 'CLOSE'
         DS    1          ; REF NUM

CLSPARST DFB   1          ; NUMBER OF PARAMETERS FOR 'CLOSE'
         DS    1          ; REF NUM

DESPARST DFB   1
         DW    TEMPNAME

PFIXPARS DFB   1
         DW    PFIXNAME

FILENAME DS    50
PFIXNAME STR   "/ANIMTEST"
TEMPNAME STR   "/RAM/TEMP"

PROMPT   STR   "FILENAME:"
